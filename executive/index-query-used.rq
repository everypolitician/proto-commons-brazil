SELECT DISTINCT ?executive ?executiveLabel ?adminArea ?adminAreaLabel ?adminAreaType ?adminAreaTypeLabel ?position ?positionLabel {
  {
    SELECT DISTINCT ?primarySort ?adminArea ?adminAreaType {
  {
    VALUES (?adminArea ?primarySort ?adminAreaType) { (wd:Q155 1 wd:Q6256) }
  } UNION {
    # Find FLACSen of this country
    ?adminArea wdt:P17 wd:Q155 ;
          wdt:P31/wdt:P279* wd:Q10864048
    VALUES (?primarySort ?adminAreaType) { (2 wd:Q10864048) }
  } UNION {
    # Find cities with populations of over 250k
    ?adminArea wdt:P17 wd:Q155 ;
       wdt:P31/wdt:P279* wd:Q515 ;
       wdt:P1082 ?population .
    FILTER (?population > 250000)
    # Make sure the city is not also a FLACS
    MINUS { ?adminArea wdt:P31/wdt:P279* wd:Q10864048 }
    VALUES (?primarySort ?adminAreaType) { (3 wd:Q515) }
  }
} ORDER BY ?primarySort

  }

  OPTIONAL {
    {
      ?position p:P1001 [ wikibase:rank ?appliesToJurisdictionRank ; ps:P1001 ?adminArea ] ;
        wdt:P31/wdt:P279* wd:Q4164871 .
      FILTER (?appliesToJurisdictionRank != wikibase:DeprecatedRank)
      FILTER EXISTS {
        VALUES ?positionSuperclass { wd:Q2285706 wd:Q30461 }
        ?position wdt:P279* ?positionSuperclass .
      }
    } UNION {
      ?adminArea wdt:P1313 ?position
    }

    OPTIONAL {
      ?position wdt:P361 ?executive .
      # Exclude executives that are privy councils
      FILTER NOT EXISTS { ?executive wdt:P31/wdt:P279* wd:Q6528244 }
      # Exclude executives which aren't direct parents of the position
      FILTER NOT EXISTS { ?position wdt:P361 ?other . ?other wdt:P361+ ?executive }
    }
  }

  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,pt". }
} ORDER BY ?primarySort ?country ?adminAreaType ?executive ?position
